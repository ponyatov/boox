\secrel{Simple Queries}\label{adv3}\secdown

Now that we have some facts in our Prolog program, we can
\termdef{consult}{\prolog!consult} the program in the listener and
\termdef{query}{\prolog!query}, or \termdef{call}{\prolog!call}, the facts. This
chapter, and the next \ref{adv4}, will assume the Prolog program contains only
facts. Queries against programs with rules will be covered in a later chapter
\ref{5}.

Prolog queries work by pattern matching. The query pattern is called a
\termdef{goal}{\prolog!goal}. If there is a fact that matches the \term{goal},
then the \term{query} \emph{succeeds} and the listener responds with
\verb'yes.'\note{or \var{true.}} If there is \emph{no matching} fact, then the
query \emph{fails} and the listener responds with \verb'no.'\note{or
\var{false.}}

Prolog's \term{pattern matching} is called
\termdef{unification}{\prolog!unification}. In the case where the logicbase
contains only facts, unification succeeds if the following three conditions hold
simultaneously.
\begin{itemize}[nosep]
  \item 
The predicate named in the goal and logicbase are the same.
  \item 
Both predicates have the same arity.
  \item 
All of the arguments are the same.
\end{itemize}

Before proceeding, review figure 3.1, which has a listing of the program so far.

\lst{Figure 3.1. The listing of \prog{Nani Search} entered at this
point}{prolog/adventure/fig31.pl}{prolog}

The first query we will look at asks if the office is a room in the game. To
pose this, we would enter that goal followed by a period at the listener prompt.
\begin{verbatim}
?- room(office).
yes
\end{verbatim}

Prolog will respond with a \verb'yes' or \verb'true.' if a match was found. If
we wanted to know if the attic was a room, we would enter that goal.
\begin{verbatim}
?- room(attic).
no
\end{verbatim}

Prolog will respond with a \verb'no' if no match was found. Likewise, we can ask
about the locations of things.
\begin{verbatim}
?- location(apple, kitchen).
yes

?- location(kitchen, apple).
no
\end{verbatim}
Prolog responds to our location query patterns in a manner that makes sense to
us. That is, the kitchen is not located in the apple.

However, here is the problem with the one-way doors, which we still haven't
fixed. It is mentioned again to stress the importance of the order of the
arguments.
\begin{verbatim}
?- door(office, hall).
yes

?- door(hall, office).
no
\end{verbatim}

Goals can be generalized by the use of Prolog
\termdef{variables}{\prolog!variable}. They do not behave like the variables in
other languages, and are better called \term{logical variables} (although Prolog
does not precisely correspond to logic). The logical variables replace one or
more of the arguments in the goal.

Logical variables add a new dimension to unification. As before, the predicate
names and arity must be the same for unification to succeed. However, when the
corresponding arguments are compared, an (unbound) \emph{variable} will
successfully \emph{match any term}.

After successful unification, the logical variable takes on the value of the
term it was matched with. This is called \termdef{binding}{\prolog!binding} the
variable. When a goal with a variable successfully unifies with a fact in the
logicbase, Prolog returns the value of the newly bound variable.

Since there may be more than one value a variable can be bound to and still
satisfy the goal, Prolog provides the means for you to ask for alternate values.
After an answer you can enter a semicolon \keys{;}. It causes Prolog to look
for alternative bindings for the variables. Entering anything else at the prompt
ends the query.

For example, we can use a logical variable to find all of the rooms.
\begin{verbatim}
?- room(X).
X = kitchen ;
X = office ;
X = hall ;
X = 'dining room' ;
X = cellar ;
no
\end{verbatim}
The last \verb'no' means there are no more answers.

Here's how to find all the things in the kitchen. (Remember, variables begin
with uppercase letters.)
\begin{verbatim}
?- location(Thing, kitchen).
Thing = apple ;
Thing = broccoli ;
Thing = crackers ;
no
\end{verbatim}

We can use two variables to see everything in every place.
\begin{verbatim}
?- location(Thing, Place).
Thing = desk
Place = office ;

Thing = apple
Place = kitchen ;

Thing = flashlight
Place = desk ;

...
no
\end{verbatim}

Other sample applications might have the following queries.

What customers live in Boston, and what is their credit rating\,?
\begin{verbatim}
?- customer(X, boston, Y).
\end{verbatim}

What is the title of chapter 2 \ref{adv2}\,?
\begin{verbatim}
?- chapter(2,Title).
\end{verbatim}

What are the coordinates of window \verb'main'\,?
\begin{verbatim}
?- window(main,Row1,Col1,Row2,Col2).
\end{verbatim}

\secrel{How Queries Work}

When Prolog tries to satisfy a goal about a predicate, such as
\verb'location/2', it searches through the clauses defining \verb'location/2'.
When it finds a match for its variables, it marks the particular clause that was
used to satisfy the goal. Then, if the user asks for more answers, it resumes
its search of the clauses at that place marker.

Referring to the list of clauses in figure 3.1, let's look closer at this
process with the query \verb'location(X, kitchen)'. First, unification is
attempted between the query pattern and the first clause of \verb'location/2'.
\begin{verbatim}
Pattern                           Clause #1 
location(X, kitchen)              location(desk, office) 
\end{verbatim}
This unification fails. The predicate names are the same, the number of
arguments is the same, but the second argument in the pattern, \verb'kitchen',
is \emph{different} from the second argument in the clause, \verb'office'.

Next, unification is attempted between the pattern and the second clause of
\verb'location/2'.
\begin{verbatim}
Pattern                           Clause #2 
location(X, kitchen)              location(apple, kitchen) 
\end{verbatim}
This unification succeeds. The predicate names, \term{arity} (number of
arguments), and second arguments \emph{are the same}. The first arguments can be
made the same if the variable \verb'X' in the pattern takes the value
\verb'apple.'

Now that unification succeeds, the Prolog listener reports its success, and the
binding of the variable \var{X}.
\begin{verbatim}
?- location(X, kitchen).
X = apple
\end{verbatim}
If the user presses a key other than the semicolon \keys{;} at this point, the
listener responds with \verb'yes' indicating the query ended successfully.

If the user presses the semicolon (;) key, the listener looks for other
solutions. First it \term{unbinds} the variable \var{X}. Next it resumes the
search using the clause following the one that had just satisfied the query.
This is called \termdef{backtracking}{\prolog!backtracking}. In the example that
would be the third clause.
\begin{verbatim}
Pattern                           Clause #3 
location(X, kitchen)              location(flashlight, desk) 
\end{verbatim}

This fails, and the search continues. Eventually the sixth clause succeeds.
\begin{verbatim}
Pattern                           Clause #6 
location(X, kitchen)              location(broccoli, kitchen) 
\end{verbatim}

As a result, the variable \var{X} is now rebound to \verb'broccoli', and the
listener responds
\begin{verbatim}
X = broccoli ;
\end{verbatim}

Again, entering a semicolon \keys{;} causes \var{X} to be unbound and the search
to continue with the seventh clause, which also succeeds.
\begin{verbatim}
X = crackers ;
\end{verbatim}

As before, entering anything except a semicolon (;) causes the listener to
respond \verb'yes', indicating success. A semicolon (;) causes the unbinding of
\var{X} and the search to continue. But now, there are no more clauses that
successfully unify with the pattern, so the listener responds with \verb'no'
indicating the final attempt has failed.
\begin{verbatim}
no 
\end{verbatim}
The best way to understand Prolog execution is to trace its execution in the
debugger. But first it is necessary to have a deeper understanding of goals.

A Prolog goal has four \termdef{ports}{\prolog!port} representing the flow of
control through the goal: call, exit, redo, and fail. First the goal is called.
If successful it is exited. If not it fails. If the goal is retried, by entering
a semicolon (;) the redo port is entered. Figure 3.2 shows the goal and its
ports.

\fig{\ Figure 3.2. The ports of a \prolog\
goal}{prolog/adventure/fig32.png}{width=0.4\textwidth}

\noindent
The behaviors at each port are
\begin{description}
\item[call]\ \\Begins searching for clauses that unify with the goal
\item[exit]\ \\Indicates the goal is satisfied, sets a place marker at the
clause and binds the variables appropriately
\item[redo]\ \\Retries the goal, unbinds the variables and resumes search at the
place marker
\item[fail]\ \\Indicates no more clauses match the goal
\end{description}

Prolog debuggers use these ports to describe the state of a query. Figure 3.3
shows a trace of the \verb'location(X, kitchen)' query. Study it carefully
because it is the key to your understanding of Prolog. The number in parentheses
indicates the current clause.

\lstx{Figure 3.3. Prolog trace of
\var{location(X,kitchen)}}{prolog/adventure/fig33.log}

Because the trace information presented in this book is designed to teach Prolog
rather than debug it, the format is a little different from that used in the
actual debugger. Run the Amzi! Source Code Debugger on these queries to see how
they work for real.

To start the \prog{Amzi! Debugger}, highlight your project name or edit a source
file in your project, then select \menu{Run>Debug As>Interpreted Project}
from the main menu.

You will see a separate perspective with multiple views that contain trace
information. Enter the query \verb'location(X, kitchen)' in the \menu{Debug
Listener} view. You will see the trace start in the debugger view.

Use the \menu{Step Over} button in the debugger to creep from port to port. When
output appears in the listener view, enter semicolons \keys{;} to continue the
search. See the help files for more details on the debugger.

Unification between goals and facts is actually more general than has been
presented. Variables can also occur in the facts of the Prolog logicbase as
well.

For example, the following fact could be added to the Prolog program. It might
mean everyone sleeps.
\begin{verbatim}
sleeps(X).
\end{verbatim}

You can add it directly in the listener, to experiment with, like this.
\begin{verbatim}
?- assert(sleeps(X)).
yes
\end{verbatim}

Queries against a logicbase with this fact give the following results.
\begin{verbatim}
?- sleeps(jane).
yes

?- sleeps(tom).
yes
\end{verbatim}
Notice that the listener does not return the variable bindings of \verb'X=jane'
and \verb'X=tom.' While they are surely bound that way, the listener only lists
variables mentioned in the query, not those used in the program.

Prolog can also bind variables to variables.
\begin{verbatim}
?- sleeps(Z).
Z = H116

?- sleeps(X).
X = H247
\end{verbatim}
When two unbound variables match, they are both bound, but \emph{not to a
value}. They are bound together, so that if either one takes a value, the other
takes the same value. This is usually implemented by binding both variables to a
common internal variable. In the first query above, both \var{Z} in the query
and \var{X} in the fact are bound to internal variable \var{H116}. In this way
Prolog remembers they have the same value. If either one is bound to a value
later on, both automatically bind to that value. This feature of Prolog
distinguishes it from other languages and, as we will discover later, gives
Prolog much of its power.

The two queries above are the same, even though one uses the same character
\var{X} that is used in the fact \verb'sleeps(X)'. The variable in the fact is
considered different from the one in the query.

\secrel{Exercises}\secdown

The exercise sections will often contain nonsense Prolog questions. These are
queries against a meaningless logicbase to strengthen your understanding of
Prolog without the benefit of meaningful semantics. You are to predict the
answers to the query and then try them in Prolog to see if you are correct. If
you are not, trace the queries to better understand them.

\secrel{Nonsense Prolog}

1- Consider the following Prolog logicbase
\begin{verbatim}
easy(1).
easy(2).
easy(3).

gizmo(a,1).
gizmo(b,3).
gizmo(a,2).
gizmo(d,5).
gizmo(c,3).
gizmo(a,3).
gizmo(c,4).
\end{verbatim}
and predict the answers to the queries below, including all alternatives when
the semicolon (;) is entered after an answer.
\begin{verbatim}
?- easy(2).
?- easy(X).

?- gizmo(a,X).
?- gizmo(X,3).
?- gizmo(d,Y).
?- gizmo(X,X).
\end{verbatim}

2- Consider this logicbase,

\begin{verbatim}
harder(a,1).
harder(c,X).
harder(b,4).
harder(d,2).
\end{verbatim}
and predict the answers to these queries.
\begin{verbatim}
?- harder(a,X).
?- harder(c,X).
?- harder(X,1).
?- harder(X,4).
\end{verbatim}

\secrel{Adventure Game}

3- Enter the listener and reproduce some of the example queries you have seen
against \verb|location/2|. List or print \verb|location/2| for reference if you
need it. Remember to respond with a semicolon \keys{;} for multiple answers.
Trace the query.

\secrel{Genealogical Logicbase}

4- Pose queries against the genealogical logicbase that:
\begin{itemize}[nosep]
  \item 
Confirm a parent relationship such as parent(dennis, diana)
  \item 
Find someone's parent such as parent(X, diana)
  \item 
Find someone's children such as parent(dennis, X)
  \item 
List all parent-children such as parent(X,Y)
\end{itemize}

\bigskip\noindent
5- If \verb|parent/2| seems to be working, you can add additional family members
to get a larger logicbase. Remember to include the corresponding \verb|male/1|
or \verb|female/1| predicate for each individual added.

\secrel{Customer Order Entry}

6- Pose queries against the customer order entry logicbase that
\begin{itemize}[nosep]
  \item 
find customers in a given city
  \item 
find customers with a given credit rating
  \item 
confirm a given customer's credit rating
  \item 
find the customers in a given city with a given credit rating
  \item 
find the reorder quantity for a given item
  \item 
find the item number for a given item name
  \item 
find the inventory level for a given item number
\end{itemize}

\secup

\secup